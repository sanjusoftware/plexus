plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'jacoco'
}

group = 'com.bankengine.catalog'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

repositories {
    mavenCentral()
    maven { url "https://repository.jboss.org/maven2/" }
}

jacoco {
    toolVersion = "0.8.11"
}

dependencies {
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    // FORCE UPGRADE for Transitive Vulnerabilities
    constraints {
        implementation('com.thoughtworks.xstream:xstream:1.4.21') {
            because 'fixes CVE-2024-47072 (High Severity DoS vulnerability)'
        }
    }

    // Drools Dependencies
    def droolsVersion = '8.44.2.Final'
    implementation "org.drools:drools-core:${droolsVersion}"
    implementation "org.drools:drools-compiler:${droolsVersion}"
    implementation "org.drools:drools-xml-support:${droolsVersion}"
    implementation "org.kie:kie-api:${droolsVersion}"
    implementation "org.kie:kie-ci:${droolsVersion}"

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'com.h2database:h2'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    runtimeOnly 'org.postgresql:postgresql'

    // Actuator for Health Checks
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Optional: Improved observability
    implementation 'io.micrometer:micrometer-observation'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
}

def coverageExclusions = [
    "**/dto/**",
    "**/model/**",
    "**/config/**",
    "**/exception/**",
    "**/*Application*",
    "**/*MapperImpl*",
    "org/drools/drl/parser/lang/**"
]

tasks.named('test') {
    useJUnitPlatform()

    jacoco {
        excludes = ["org/drools/drl/parser/lang/*"]
    }

    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusions)
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        // Global rule: 85% for the whole project
        rule {
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.85
            }
            excludes = coverageExclusions
        }

        // Strict rule: 95% specifically for Pricing Logic
        rule {
            element = 'PACKAGE'
            includes = ['com.bankengine.pricing.service*']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.95
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification